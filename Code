#include <LiquidCrystal.h>
#include <EEPROM.h>

/* ================= LCD ================= */
LiquidCrystal lcd(4, 6, 7, 8, 9, 10);

/* ================= BUTTONS ================= */
const int numOfInputs = 4;
const int inputPins[numOfInputs] = {5, 3, 11, 12}; // LEFT, RIGHT, ENTER, STOP
bool inputFlags[numOfInputs] = {LOW, LOW, LOW, LOW};

/* ================= MENU ================= */
const int numOfScreens = 6;
int currentScreen = 0;

String screens[numOfScreens][2] = {
  {"R U Thirsty?", ""},
  {"1 Glass", "Press Enter"},
  {"100 mL", "Press Enter"},
  {"250 mL", "Press Enter"},
  {"500 mL", "Press Enter"},
  {"1 L", "Press Enter"}
};

/* ================= FLOW SENSOR ================= */
volatile int counter_sensor = 0;
const int sensor_pin = 2;

/* ================= MOTOR & BUZZER ================= */
const int motorA = A5;
const int motorB = A6;
const int buzzer_pin = 13;
const int Stop_Pin = 12;

/* ================= EEPROM ================= */
int water_rem = 0;

/* ================= SETUP ================= */
void setup() {
  pinMode(motorA, OUTPUT);
  pinMode(motorB, OUTPUT);
  pinMode(buzzer_pin, OUTPUT);

  for (int i = 0; i < numOfInputs; i++) {
    pinMode(inputPins[i], INPUT_PULLUP);
  }

  pinMode(sensor_pin, INPUT_PULLUP);

  Serial.begin(9600);
  lcd.begin(16, 2);

  water_rem = readIntFromEEPROM(45);

  welcomeScreen();
  printScreen();
}

/* ================= LOOP ================= */
void loop() {
  for (int i = 0; i < numOfInputs; i++) {
    if (digitalRead(inputPins[i]) == LOW) inputFlags[i] = HIGH;
  }
  resolveInputFlags();
  delay(150);
}

/* ================= INPUT HANDLING ================= */
void resolveInputFlags() {
  for (int i = 0; i < numOfInputs; i++) {
    if (inputFlags[i]) {
      inputAction(i);
      inputFlags[i] = LOW;
      printScreen();
    }
  }
}

void inputAction(int input) {
  if (input == 0)
    currentScreen = (currentScreen == 0) ? numOfScreens - 1 : currentScreen - 1;
  else if (input == 1)
    currentScreen = (currentScreen == numOfScreens - 1) ? 1 : currentScreen + 1;
  else if (input == 2)
    start_Dispense();
}

/* ================= DISPENSE ================= */
void start_Dispense() {
  if (currentScreen == 1) Dispense(70);
  if (currentScreen == 2) Dispense(100);
  if (currentScreen == 3) Dispense(250);
  if (currentScreen == 4) Dispense(500);
  if (currentScreen == 5) Dispense(1000);
}

void Dispense(int ml) {
  bool userStopped = false;

  counter_sensor = 0;
  attachInterrupt(digitalPinToInterrupt(sensor_pin), Flow, FALLING);
  Motor_On();

  lcd.clear();
  lcd.print("Dispensing...");

  while (1) {
    Serial.print("Pulse Count: ");
    Serial.println(counter_sensor);

    if (digitalRead(Stop_Pin) == LOW) {
      userStopped = true;
      break;
    }

    if (
      (ml == 70   && counter_sensor >= 400) ||
      (ml == 100  && counter_sensor >= 140) ||
      (ml == 250  && counter_sensor >= 350) ||
      (ml == 500  && counter_sensor >= 700) ||
      (ml == 1000 && counter_sensor >= 1400)
    ) break;
  }

  detachInterrupt(digitalPinToInterrupt(sensor_pin));
  Motor_Off();

  if (!userStopped) {
    water_rem += ml;
    writeIntIntoEEPROM(45, water_rem);
    lcdMessage("Dispense Done", "Thank You!");
  } else {
    lcdMessage("Stopped by User", "Thank You!");
  }

  digitalWrite(buzzer_pin, HIGH);
  delay(700);
  digitalWrite(buzzer_pin, LOW);

  delay(2000);
  currentScreen = 0;
  printScreen();
}

/* ================= LCD MESSAGE ================= */
void lcdMessage(String l1, String l2) {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print(l1);
  lcd.setCursor(0, 1);
  lcd.print(l2);
}

/* ================= FLOW ISR ================= */
void Flow() {
  counter_sensor++;
}

/* ================= DISPLAY ================= */
void printScreen() {
  lcd.clear();
  lcd.print(screens[currentScreen][0]);
  lcd.setCursor(0, 1);
  lcd.print(screens[currentScreen][1]);
}

void welcomeScreen() {
  lcd.clear();
  lcd.print("Welcome User");
  lcd.setCursor(0, 1);
  lcd.print("R U Thirsty?");
  delay(2500);
}

/* ================= MOTOR ================= */
void Motor_On() {
  digitalWrite(motorA, HIGH);
  digitalWrite(motorB, LOW);
}

void Motor_Off() {
  digitalWrite(motorA, LOW);
  digitalWrite(motorB, LOW);
}

/* ================= EEPROM ================= */
void writeIntIntoEEPROM(int address, int number) {
  EEPROM.write(address, number >> 8);
  EEPROM.write(address + 1, number & 0xFF);
}

int readIntFromEEPROM(int address) {
  return (EEPROM.read(address) << 8) + EEPROM.read(address + 1);
}
